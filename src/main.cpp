/*********
  Rui Santos
  Complete project details at https://randomnerdtutorials.com  
*********/

// Import required libraries

#include "WiFi.h"
#include "ESPAsyncWebServer.h" 
#include "SPIFFS.h"
#include <Adafruit_MCP23X17.h>  

#define SDA_1 1                       // I choose these pins for I2C.
#define SCL_1 0
Adafruit_MCP23X17 mcp; 

// Replace with your network credentials
const char* ssid = "SSID";
const char* password = "psw";

// Set LED GPIO
int ledPin = 2;                // MCP23017 pin LED

// Stores LED state
String ledState;

// Create AsyncWebServer object on port 80
AsyncWebServer server(80);

// Replaces placeholder with LED state value
String processor(const String& var){
  Serial.println(var);
  if(var == "STATE"){
    if(mcp.digitalRead(ledPin)){
      ledState = "ON";
    }
    else{
      ledState = "OFF";
    }
    Serial.print(ledState);
    return ledState;
  }
  return String();
}
 
void setup(){
  // Serial port for debugging purposes
  Serial.begin(115200);
  Wire.begin(SDA_1 , SCL_1 , 100000UL);
  mcp.begin_I2C();
    delay(3000);
 
    
    mcp.pinMode(ledPin, OUTPUT);
    mcp.digitalWrite(ledPin, LOW);



  // Initialize SPIFFS
  if(!SPIFFS.begin(true)){
    Serial.println("An Error has occurred while mounting SPIFFS");
    return;
  }

  // Connect to Wi-Fi
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.println("Connecting to WiFi..");
  }

  // Print ESP32 Local IP Address
  Serial.println(WiFi.localIP());

  // Route for root / web page
  server.on("/", HTTP_GET, [](AsyncWebServerRequest *request){
    request->send(SPIFFS, "/index.html", String(), false, processor);
  });
  
  // Route to load style.css file
  server.on("/style.css", HTTP_GET, [](AsyncWebServerRequest *request){
    request->send(SPIFFS, "/style.css", "text/css");
  });

  // Route to set GPIO to HIGH
  server.on("/on", HTTP_GET, [](AsyncWebServerRequest *request){
    mcp.digitalWrite(ledPin, HIGH);    
    request->send(SPIFFS, "/index.html", String(), false, processor);
  });
  
  // Route to set GPIO to LOW
  server.on("/off", HTTP_GET, [](AsyncWebServerRequest *request){
    mcp.digitalWrite(ledPin, LOW);    
    request->send(SPIFFS, "/index.html", String(), false, processor);
  });

  // Route to show log.html
  server.on("/html", HTTP_GET, [](AsyncWebServerRequest *request){
    request->send(SPIFFS, "/log.html", "text/html");
  });

  // Route to show text.txt
  server.on("/quote", HTTP_GET, [](AsyncWebServerRequest *request){
    request->send(SPIFFS, "/quote.html", "text/html");
  });

   // Route to show text.txt
  server.on("/data", HTTP_GET, [](AsyncWebServerRequest *request){
    request->send(SPIFFS, "/log.txt", "text/plain");
  });

  // Start server
  server.begin();
}
 
void loop(){
  
}